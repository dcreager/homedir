#!/usr/bin/perl
# -*- Mode: Perl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 2 -*-

use strict;
use warnings;

use Data::Dumper;

use FileHandle;
use Getopt::Std;
use Memoize;
use Pod::Usage;
use Sys::Hostname;
use Term::ANSIColor;

# caching
memoize( 'get_port_variants'     );
memoize( 'get_port_dependencies' );
memoize( 'get_port_dependents'   );

   $|                  = 1;     # output immediately
my %opt;
my $ok_opts            = getopts( 'ac:fhnsu', \%opt );
my $DEFAULTS_FILE      =    $opt{'c'}
                         || $ENV{'PINST_DEFAULTS'} 
                         || "$ENV{'HOME'}/etc/ports";
my $DEPENDENCIES_CMD   = 'port deps';
my $DEPENDENTS_CMD     = 'port dependents';
my $INSTALLED_CMD      = 'port installed';
my $VARIANTS_CMD       = 'port variants';
my $INSTALL_CMD        = 'port install';
my $UNINSTALL_CMD      = 'port uninstall';
my $hostname           = defined $ENV{'HOST'} 
                           ? $ENV{'HOST'}
                           : hostname();
   $hostname          =~ s/\..*//;

my %installed_ports    = get_installed_ports();
my( $default_ports,
    @ports_order )     = get_default_ports();
my @ports              = @ARGV;


if ( !$ok_opts  or  defined $opt{'h'} ) {
    pod2usage();
}

if ( defined $opt{'a'} ) {
    list_additional_ports();
}
else {
    
    if ( -1 == $#ports ) {
        @ports = @ports_order;
    }

    foreach my $port ( @ports ) {
        if ( defined $opt{'u'} ) {
            uninstall_port( $port );
        }
        else {
            install_port( $port );
        }
    }

}
exit;


sub list_additional_ports {
    my %justified_ports;
    my %unjustified_ports;
    
    status( 'Calculating dependencies: ' );
    foreach my $port ( @ports_order ) {
        my %dependencies = get_full_dependencies( $port );
        foreach my $dependency ( keys %dependencies ) {
            $justified_ports{$dependency} = 1;
        }
        $justified_ports{$port} = 1;
    }
    status( "\n\n" );
    
    foreach my $port ( keys %installed_ports ) {
        if ( !defined $justified_ports{$port} ) {
            $unjustified_ports{$port} = 1;
        }
    }
    
    my %depth;
    my $highest = 1;
    my %tree = get_port_dependents_tree( %unjustified_ports );
    foreach my $port ( keys %unjustified_ports ) {
        my $depth = get_dependency_depth( $port, \%tree );
        
        $depth{$port} = $depth;
        if ( $depth > $highest ) {
            $highest = $depth;
        }
    }
    
    foreach my $port ( keys %depth ) {
        if ( $highest == $depth{$port} ) {
            describe_dependency_tree( $port, \%tree );
        }
    }
}
sub describe_dependency_tree {
    my $port  = shift;
    my $tree  = shift;
    my $depth = shift || 0;
    
    my $indent    = q(    ) x ( $depth - 1 );
    my $indicator = $depth ? q( -> ) : q();
    print "${indent}${indicator}${port}\n";
    foreach my $dep ( keys %{ $tree->{$port} } ) {
        describe_dependency_tree( $dep, $tree, $depth+1 );
    }
}
sub get_dependency_depth {
    my $port  = shift;
    my $tree  = shift;
    
    my $children   = 0;
    my @dependents = keys %{ $tree->{$port} };
    foreach my $dep ( @dependents ) {
        my $value = get_dependency_depth( $dep, $tree );
        
        if ( $value > $children ) {
            $children = $value;
        }
    }
    
    return $children + 1;
}
sub get_port_dependents_tree {
    my %ports = @_;
    my %tree;
    
    foreach my $port ( keys %ports ) {
        foreach my $dep ( get_port_dependents( $port ) ) {
            $tree{$dep}{$port} = 1;
        }
    }

    return %tree;
}
sub get_full_dependencies {
    my $port  = shift;
    my %dependencies;
    
    foreach my $dep ( get_port_dependencies( $port ) ) {
        my %sub_deps = get_full_dependencies( $dep );
        $dependencies{$dep} = 1;
        foreach my $sub_dep ( keys %sub_deps ) {
            $dependencies{$sub_dep} = 1;
        }
    }
    status( q(.) );

    return %dependencies;
}
sub install_port {
    my $port      = shift;
    my $dependent = shift;
    
    return if defined $installed_ports{$port};
    
    foreach my $dependency ( get_port_dependencies( $port ) ) {
        install_port( $dependency, $port );
    }
    
    my @variants       = get_port_variants( $port );
    my $cannot_install =    ( 0 < $#variants )
                         && !defined $default_ports->{$port}
                         && !defined $opt{'f'};
    my $colour         = 'green';
    my $install_text;
    
    if ( defined $dependent ) {
        $install_text = "install $dependent dependency $port";
        $colour       = 'yellow';
    }
    else {
        $install_text = "install $port";
    }
    
    if ( $cannot_install ) {
        print colored "Cannot ${install_text} because it has variants:\n    ", 
                      'bold red';
        print join "\n    ", @variants;
        print "\n\n";
        exit( 1 )
            unless defined $opt{'n'};
    }

    print colored "About to ${install_text}:\n", "bold ${colour}";
    
    my $command = "${INSTALL_CMD} $port ";
    if ( defined $default_ports->{$port}  ) {
        $command .= join ' ', keys %{ $default_ports->{$port}{'variants'} };
    }
    
    if ( defined $opt{'n'} ) {
        print "**  $command\n";
    }
    else {
        my $return = system( $command );
        die if $return;
    }
    
    $installed_ports{$port}{'version'} = 'unknown';
    print "\n";
}
sub uninstall_port {
    my $port       = shift;
    my $dependency = shift;
    
    return unless defined $installed_ports{$port};

    foreach my $dependent ( get_port_dependents( $port ) ) {
        uninstall_port( $dependent, $port );
    }
    
    my $colour       = 'green';
    my $install_text = defined $opt{'d'} 
                           ? 'deactivate '
                           : 'uninstall ';
    if ( defined $dependency ) {
        $install_text .= "$dependency dependent $port";
        $colour        = 'yellow';
    }
    else {
        $install_text .= "$port";
    }
        
    print colored "About to ${install_text}:\n", "bold ${colour}";

    my $command = "${UNINSTALL_CMD} $port";
    if ( defined $opt{'n'} ) {
        print "**  $command\n";
    }
    else {
        my $return  = system( $command );
        die if $return;
    }
    
    delete $installed_ports{$port};
}
sub get_port_dependencies {
    my $port     = shift;
    my $variants = '';
    my @dependencies;
    
    if ( defined $default_ports->{$port} ) {
        $variants = join ' ', keys %{ $default_ports->{$port}{'variants'} };
    }

    my $handle = FileHandle->new( "${DEPENDENCIES_CMD} ${port} ${variants} |" )
                 or die "$!";
    
    LINE:
    while( my $line = <$handle> ) {
        if ( $line =~ m{ ^ [^\s]+ \s+ has }x ) {
            # ignore
        }
        elsif ( $line =~ m{ ( [^\s]+ ) }x ) {
            push @dependencies, $1;
        }
        else {
            die "Dependency error: $line";
        }
    }
    
    return @dependencies;
}
sub get_port_dependents {
    my $port = shift;
    my @dependents;
    
    my $handle = FileHandle->new( "${DEPENDENTS_CMD} ${port} | " )
                 or die "$!";
    
    LINE: 
    while ( my $line = <$handle> ) {
        if ( $line =~ m{ ^ ( [^\s]+ ) \s depends \s on \s $port }x ) {
            push @dependents, $1;
        }
        elsif( $line =~ m{ has \s no\s dep }x ) {
            # do nothing
        }
        else {
            die "Dependents error: $line";
        }
    }
    
    return @dependents;
}
sub get_installed_ports {
    my %ports;
    my $installed_regexp = qr{ 
        ^
            [ ][ ]
            ( [^\s]+ )          # capture the port
            \s+
            [@]
            ( [\d\w._-]+ )      # capture the version
            (                   # capture any variants
                [+\w]+
            )*
            (?:                 # optional active tag
                \s+
                \(active\)
            )*
            \s*
        $
    }xs;
    my $handle = FileHandle->new( "${INSTALLED_CMD} |" )
                 or die "$!";

    status( 'Getting installed ports: ' );
    # skip the first line - always an intro line
    <$handle>;
    
    while( my $line = <$handle> ) {
        if ( $line =~ $installed_regexp ) {
            my $port     = $1;
            my $version  = $2;
            my $variants = $3;
            
            $ports{$port}{'version'} = $version;

            if ( defined $variants ) {
                my @variants = split /\+/, $variants;
                shift @variants;
                
                foreach my $variant ( @variants ) {
                    $ports{$port}{'variants'}{$variant}= 1;
                }
            }
            status( q(.) );
        }
        else {
            die "UNMATCHED: $line";
        }
    }
    status( "\n" );
    
    return %ports;
}
sub get_port_variants {
    my $port = shift;
    my @variants;
    
    my $handle = FileHandle->new( "${VARIANTS_CMD} $port |" )
                 or die "${VARIANTS_CMD}: $!";
    
    # skip the first line - always an intro line
    <$handle>;
    
    my $variant_regexp = qr{
        ^
            \s+
            ( [^\s]+ )      # capture the port
            (?:             # optional description
                :
                \s+
                .*
            )?
        $
    }x;
    
    LINE:
    while ( my $line = <$handle> ) {
        # skip the common variants (they don't apply)
        next LINE if $line =~ m{ ^ \s+ darwin :? }x;
        next LINE if $line =~ m{ ^ \s+ universal :? }x;
        
        if ( $line =~ $variant_regexp ) {
            push @variants, $1;
        }
        else {
            die "Variants format error: $line";
        }
    }
    
    return @variants;
}
sub get_default_ports {
    my $handle = FileHandle->new( $DEFAULTS_FILE )
                 or die "${DEFAULTS_FILE}: $!";
    
    my $assignment_regexp = qr{
        ^
            \s*
            ( [A-Z0-9_]+ )  # key
            \s* = \s*
            (.*)?           # value
            \s*
        $
    }x;
    my $port_regexp = qr{
        ^
            ( [^\s]+ )      # applicable hosts
            \s+
            ( [^\s]+ )      # port
            (?:
                \s* (.*)?   # optional variants
            )?
            \s*
        $
    }x;
    my %assignments;
    my %ports;
    my @order;
    
    LINE:
    while ( my $line = <$handle> ) {
        # remove comments and skip blank linkes
        $line =~ s{ [#] .* $ }{}x;
        next LINE if $line =~ m{ ^ \s* $ }x;
        
        if ( $line =~ $assignment_regexp ) {
            $assignments{$1} = $2;
        }
        elsif ( $line =~ $port_regexp ) {
            my $hosts    = $1;
            my $port     = $2;
            my $variants = $3 || q();
            
            # replace assignments
            foreach my $key ( keys %assignments ) {
                my $value = $assignments{$key};
                $hosts =~ s{$key}{$value};
            }
            
            foreach my $host ( split /,/, $hosts ) {
                if ( $hostname eq $host  or  q(*) eq $host ) {
                    push @order, $port;
                    $ports{$port}{'install'} = 1;

                    foreach my $var ( split /\s+/, $variants ) {
                        $ports{$port}{'variants'}{$var} = 1;
                    }
                }
            }
        }
        else {
            die "File format error:\n$line";
        }
    }
    
    return \%ports, @order;
}
sub status {
    my $text = shift;
    
    print $text if defined $opt{'s'};
}


__END__

# TODO
#   *   tag support to be able to install a subset of the default file


=head1 NAME

B<pinst> - MacPorts install helper

=head1 SYNOPSIS

B<pinst> [B<-c> I<file>] [B<-fns>] [I<port> I<...>]

B<pinst> [B<-c> I<file>] B<-a>

B<pinst> B<-h>

=head1 DESCRIPTION

Dependencies in MacPorts are automatically handled, so why write a dependency
wrapper program? Two reasons:

=over

=item 1.

sometimes MacPorts installs dependencies a little too aggressively. For 
example, C<php5> requires C<gd> which, by default, installs C<XFree86>. 
I don't want all of X11 rebuilt just to get PHP.

=item 2.

by having a defaults file which lists required packages, I can reinstall
the packages I want as I set up a new OS X machine without having to remember
what they are.

=back

=head1 OPTIONS

=over

=item B<-h>

display the synopsis and exit

=item B<-a>

list ports added to the machine that aren't specified in the defaults file, 
or a dependency of one; output is in tree form to illustrate what was 
installed as a dependency of something else

=item B<-c> I<file>

use I<file> as the defaults file

=item B<-f>

force dependencies even if it has variants

=item B<-s>

print status during operations that are likely to take some time

=back

=head1 DEFAULTS FILE

=head2 FILE LOCATION

The defaults file normally lives at F<~/etc/ports>, but can be changed
with the B<-c> argument or by setting the environment variable 
C<PINST_DEFAULTS>

=head2 FILE FORMAT

The defaults file is a simple text file. Comments are introduced with 
the normal hash symbol (#), anything from the symbol to the end of the line 
is ignored.

The file looks like this:

    # example config file:
    
    LAPTOPS=sinister,kittaghy       # laptops have identical config
    
    LAPTOPS         fortune
    *               perl5 +threads
    sinister        git-core

There are two types of entry in the file.

=head3 Package

Each package line contains:

=over

=item *

a comma-delimited list of hosts to which this package applies, which can 
include an assignment (see below), or an asterisk symbol (*) which means
the package applies to all hosts

=item *

the package name

=item *

an optional list of variants

=back

=head3 Assignments

An assigment is a line that has an uppercase word (which can also include
numerals and underscores), an equals sign and then a commaa-delimited list of
hostnames. This word can then be used in the package declarations instead of
repeating the same hostnames over and over.

=head1 AUTHOR

Mark Norman Francis <norm@cackhanded.net>
